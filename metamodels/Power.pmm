`ProtoScale Power System Metamodel`
 
module Power:

`--------|--------------------------------------------------------------------`
`Bus Node|                                                                    `
`--------|                                                                    `
`                                                                             `
` This node represents a bus within a power system                            `
`                                                                             `
`-----------------------------------------------------------------------------`
node Bus:
  S, V, Y complex;           `power, voltage, admittance`
  p, q  := re(S),  im(S);    `real, reactive power`
  v, va := mag(V), angle(V); `voltage magnitude and angle`
  y, ya := mag(V), angle(Y); `admittance magnitude angle`
  g, b  := re(Y),  im(Y);    `conductance susceptance`

  `how a bus interacts with other buses via transmission line`
  bus(ij Line, j Bus):
    y += y.ij;
    p += g*v^2 + v*v.j*y.ij*cos(ya.ij + va.j - va);
    q += b*v^2 + v*v.j*y.ij*sin(ya.ij + va.j - va);
  ::

  `how a bus interacts with generators and loads via transmission line`
   gen(ij Line, j Generator): S += S.j; ::
   load(ij Line, j Load):     S += S.j; ::
::`~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~`



`----------|------------------------------------------------------------------`
`Line link |                                                                  `
`----------|                                                                  `
`                                                                             `
` This link represents a transmission line within a power system              `
`                                                                             `
`-----------------------------------------------------------------------------`
link Line{y, ya}:
  Y complex;
  y, ya := mag(Y), angle(Y);
  metric <- 1/y;
::`~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~`



`---------------|-------------------------------------------------------------`
`Generator Node |                                                             `
`---------------|                                                             `
`                                                                             `
` This node represents a generator within a power system                      `
`                                                                             `
`-----------------------------------------------------------------------------`
node Generator:
  S complex;
  p, q := re(S), im(S);

  `a generator must be connected to a source (prime mover) to generate power`
  `note: this model is oversimplified and not realistic`
  src(ij Line, i Source):
    p = f.i * cos(t); 
    q = f.i * sin(t);
  ::
::`~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~`



`------------|----------------------------------------------------------------`
`Source Node |                                                                `
`------------|                                                                `
`                                                                             `
` This node represents a source (prime mover) that drives a generator         `
`                                                                             `
`-----------------------------------------------------------------------------`
node Source{p}:
  f, p real; `force, target operating point`

  `simulated operating point` 
  `gaussian distribution centered at p with alpha=0.5`
  i <- gaussian(p, 0.05); 

  `diff eqtn (of time t) describing evolution of force`
  f'/t = f + 0.03*(p - i); 
::`~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~`



`----------|------------------------------------------------------------------`
`Load Node |                                                                  `
`----------|                                                                  `
`                                                                             `
` This node represents a load within a power system                           `
`                                                                             `
`-----------------------------------------------------------------------------`
node Load{fp}:
  S complex;
  fp real;

  f <- gaussian(p, 2);

  p, q := re(S), im(S);

  bus(ij Line, j Bus): S = f*V.j; ::
::`~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~`

:: `End of Power Module`

